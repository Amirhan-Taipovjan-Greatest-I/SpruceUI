plugins {
    id 'fabric-loom' version '0.8-SNAPSHOT'
    id 'java-library'
    id 'maven-publish'
    id 'org.cadixdev.licenser' version '0.5.0'
}

group = project.maven_group
version = "${project.mod_version}+${getMCVersionString()}"
archivesBaseName = project.archives_base_name

// This field defines the Java version your mod target.
def targetJavaVersion = 16

Set<String> modules = [
        'fabric-api-base',
        'fabric-lifecycle-events-v1',
        'fabric-rendering-v1',
        'fabric-resource-loader-v0',
        'fabric-screen-api-v1'
]

def getMCVersionString() {
    if (project.minecraft_version.matches("\\d\\dw\\d\\d[a-z]")) {
        return project.minecraft_version
    }
    int lastDot = project.minecraft_version.lastIndexOf('.')
    return project.minecraft_version.substring(0, lastDot);
}

sourceSets {
    testmod {
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
    }
}

minecraft {
}

repositories {
    mavenLocal()
    maven {
        name = 'AperLambda'
        url = 'https://aperlambda.github.io/maven'
    }
    maven {
        name = 'TerraformersMC'
        url = 'https://maven.terraformersmc.com/releases'
    }
}

configurations {
    api.extendsFrom libApi
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modules.stream().map { fabricApi.module(it, project.fabric_api_version) }.forEach {
        modImplementation it
    }

    modRuntime("com.terraformersmc:modmenu:${project.modmenu_version}") {
        transitive = false
    }

    libApi "org.jetbrains:annotations:20.1.0"
    libApi("org.aperlambda:lambdajcommon:1.8.1") {
        exclude group: 'com.google.code.gson'
        exclude group: 'com.google.guava'
    }
    include "org.aperlambda:lambdajcommon:1.8.1"

    testmodImplementation project(":")
}

java {
    sourceCompatibility = JavaVersion.toVersion(targetJavaVersion)
    targetCompatibility = JavaVersion.toVersion(targetJavaVersion)

    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"

    it.options.release.set(targetJavaVersion)
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

runClient {
    classpath(sourceSets.testmod.runtimeClasspath)
}

license {
    header file('HEADER')
    include '**/*.java'
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }

            pom {
                name = "SpruceUI"
                description = "A Fabric Minecraft API mod which adds some GUI utilities."
            }

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.libApi.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')

                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                    dependencyNode.appendNode('scope', 'compile')
                }
            }
        }
    }

    repositories {
        mavenLocal()
        maven {
            name = "BuildDirLocal"
            url = "$buildDir/repo"
        }
        maven {
            name = "GithubPackages"
            url = uri("https://maven.pkg.github.com/LambdAurora/SpruceUI")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
}
